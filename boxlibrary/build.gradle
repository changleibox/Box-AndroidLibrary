apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
/*
 * Copyright © 2017 CHANGLEI. All rights reserved.
 */

android {
    compileSdkVersion 28
    resourcePrefix "box"    //这个随便填

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28

        multiDexEnabled true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

allprojects {
    tasks.withType(Javadoc) {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            links "http://docs.oracle.com/javase/7/docs/api"
        }
    }
    repositories {
        mavenCentral()
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    // json解析
    api 'com.google.code.gson:gson:2.8.2'
    // 图片加载
    api 'com.squareup.picasso:picasso:2.71828'
    // 网络请求
    api 'com.loopj.android:android-async-http:1.4.9'
    // daimajia.slider 主要实现绚丽的轮播图
    api 'com.nineoldandroids:library:2.4.0'
    api 'com.daimajia.slider:library:1.1.5@aar'
    // Umeng统计
    api 'com.umeng.analytics:analytics:6.1.3'
    // 下拉刷新
    api 'com.yalantis:phoenix:1.2.3'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

tasks.withType(Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        links 'http://docs.oracle.com/javase/7/docs/api'
    }
}

javadoc {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
        author true
        version true
        failOnError false
        links 'http://docs.oracle.com/javase/7/docs/api'
        title 'BoxLibrary'
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def siteUrl = 'https://github.com/changleibox/Box-AndroidLibrary' // 项目的主页
def gitUrl = 'https://github.com/changleibox/Box-AndroidLibrary.git' // Git仓库的url

group = "me.box.library" // Maven Group ID for the artifact，一般填你唯一的包名
version = "1.6.2" // This is the library version used when deploying the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'library for Android' //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'box'    //填写的一些基本信息
                        name 'changlei'
                        email 'changleibox@sina.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "android-framework"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
